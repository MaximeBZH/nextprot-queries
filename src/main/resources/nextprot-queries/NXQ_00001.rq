#id:NXQ_00001
#title:Proteins phosphorylated and located in the cytoplasm
#comment:In this query we use the keyword "Phosphoprotein" (KW-0597) and the UniProt subcellular location ontology term "Cytoplasm" (SL-0886).
#comment:Using the "childOf" allows to select for subcellular locations that are, in these ontologies, children of cytoplasm like for example "Cell cortex".
#tags:subcellular location,cellular component,PTM,phosphorylation,tutorial
#acs:A1A4S6,A1KZ92,A1L020,A1X283,A2RRP1,A2RU30,A5PLL1
#count:7795

select distinct ?entry where {
  values ?cytoloc {cv:SL-0086 cv:GO_0005737} # SL and GO values for cytoplasm
  ?entry :isoform ?iso.
  ?iso :keyword / :term cv:KW-0597. # Phosphorylated
  ?iso :cellularComponent ?loc .
  ?loc :term /:childOf ?cytoloc.
  filter not exists {?loc  :negativeEvidence ?negev} # No negative localization evidence
}