#id:NXQ_00058
#title:Proteins which are located on the genome next to a protein which is involved in spermatogenesis
#comment:In the future we will add / pre-calculate interesting predicates like :neighbor , :overlaps, ...
#tags:chromosomal location,function,tutorial
#acs:P50993,Q2M3M2,Q9NP85
#count:1071
#time:7

select distinct ?neighbour (str(?gen) as ?spergen) where {
  {
  select distinct ?entry ?chr ?gen ?sperstart (MAX(?ldist) as ?lmin) (MIN(?rdist)  as ?rmin) where {
     ?entry :isoform / :function ?func .
	 ?func :term / :childOf cv:GO_0007283 . # Involvment in spermatogenesis
	 filter not exists {?func  :negativeEvidence ?negev}
     ?entry :gene ?gene.
     ?gene :begin ?sperstart; :end ?e; :chromosome ?chr; :name ?gen.
     ?ngen :chromosome ?chr. 
     ?ngen :begin ?n_begin; :end ?n_end .
     filter ( ?n_end > (?sperstart - 1000000) && ?n_begin <= (?sperstart + 1000000)  && ?ngen != ?gene)
     bind(?sperstart - ?n_begin as ?dist)
     bind(if(?dist<0,?dist,-1000000) as ?ldist)
     bind(if(?dist>0,?dist,1000000) as ?rdist)
     }
  }
  bind(?sperstart - ?lmin as ?lchrpos)
  ?lgene :begin ?lchrpos; :chromosome ?chr; :name ?lgen.
  bind(?sperstart - ?rmin as ?rchrpos)
  ?rgene :begin ?rchrpos; :chromosome ?chr; :name ?rgen.
  { ?neighbour :gene ?lgene. }
	   union
  { ?neighbour :gene ?rgene. }
} order by ?gen