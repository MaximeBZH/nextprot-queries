#id:NXQ_00078
#title:Proteins which have been identified in at least one proteomics set and that are secreted
#tags:subcellular location,cellular component,proteomics,mass spectrometry,tutorial
#acs:P40933, A1L453, A6NNL5, ...
#count:2222
#time: 108

select distinct ?entry where {
  values ?sloc {cv:GO_0005576 cv:SL-0243} # GO and SL values for secreted
  ?entry :isoform ?iso.
  ?iso :cellularComponent ?loc .
  ?loc :term/:childOf ?sloc .
  filter not exists {?sloc  :negativeEvidence ?negev} # No negative localization evidence
  filter not exists {?iso :cellularComponent /:term cv:GO_0070062 .
					 filter not exists {?iso :cellularComponent /:term /:childOf  cv:SL-0243 .}
					} # filters out extracellular exosome only location (2758 entries)
  ?iso :peptideMapping ?map.
}
