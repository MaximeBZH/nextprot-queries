#id:NXQ_00099
#title:Secreted proteins that have at least one PTM in a position of a variant
#tags:cellular component,PTM,variant,sequence variation,subcellular location,tutorial
#acs:Q9UPT9,Q9UPN4,Q9Y272
#count:3539/1761 
#time:55.7  

select distinct ?entry where {
  values ?sloc {cv:GO_0005576 cv:SL-0243} # GO and SL values for secreted
  ?entry :isoform ?iso .
  ?iso :cellularComponent / :term /:childOf ?sloc. # secreted
  #filter not exists {?iso :cellularComponent /:term cv:GO_0070062 .
  #					 filter not exists {?iso :cellularComponent /:term /:childOf  cv:SL-0243 .}
  #					} # you can uncomment this to filters out extracellular exosome only location (2240 entries), most of them evidenced only by large-scale proteomic analysis
  ?iso :swissprotDisplayed true . # restricted to main isoform due to computing time (> 98% of sites)
  ?iso :variant /:start ?varpos.
  ?iso :ptm /:start ?varpos.
}
