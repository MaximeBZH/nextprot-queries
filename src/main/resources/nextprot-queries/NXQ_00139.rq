#id:NXQ_00139
#title:Protein kinases which are drug targets according to CHEMBL
#tags:drug,federated query,CHEMBL,tutorial
#acs:O00141,O00238,O00311
#count:413
#time:116
#comment: CHEMBL won't return more than 1000 rows
PREFIX cco: <http://rdf.ebi.ac.uk/terms/chembl#>

select distinct ?entry ?gen where {
  SERVICE<http://www.ebi.ac.uk/rdf/services/chembl/sparql>{
        SELECT distinct ?protein WHERE {
  		?target cco:hasTargetComponent ?tarComp . 
  		?target cco:taxonomy <http://identifiers.org/taxonomy/9606> .
		?tarComp cco:targetCmptXref ?protein . 
		filter(contains(str(?protein),"uniprot"))
		?activity a cco:Activity ; cco:hasMolecule ?drug ; cco:hasAssay ?assay .
		?assay cco:hasTarget ?target .
        }
     limit 1550 #experimentally set to maximize results
  	 }
  ?entry skos:exactMatch ?protein .	    
  ?entry :isoform / :keyword / :term cv:KW-0418. #kinase	
  ?entry :gene / :name ?gen.
}
