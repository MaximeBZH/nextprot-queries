#id:NXQ_00139
#title:Protein kinases which are high-confidence drug targets according to CHEMBL
#tags:drug,federated query,CHEMBL,tutorial
#acs:P36888,P06493,P27361
#count:72
#time:5.5

PREFIX cco: <http://rdf.ebi.ac.uk/terms/chembl#>

select distinct ?entry ?gen where {
  SERVICE<http://www.ebi.ac.uk/rdf/services/chembl/sparql>{
        SELECT distinct ?protein WHERE {
  		?target cco:hasTargetComponent ?tarComp . 
  		?target cco:taxonomy <http://identifiers.org/taxonomy/9606> . # human protein target
		?tarComp cco:targetCmptXref ?protein . 
		?protein a cco:UniprotRef .
		?activity a cco:Activity ; cco:hasMolecule ?drug ; cco:hasAssay ?assay .
		?drug cco:hasMechanism / cco:hasTarget ?target . # known drug action mechanism
		?assay cco:hasTarget ?target; cco:targetConfScore ?score .
		filter(?score > 8) # high-confidence assay
        }
  	 }
  ?entry skos:exactMatch ?protein .	    
  ?entry :isoform / :keyword / :term cv:KW-0418. #kinase	
  ?entry :gene / :name ?gen.
}
