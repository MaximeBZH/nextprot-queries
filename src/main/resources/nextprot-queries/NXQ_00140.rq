#id:NXQ_00140
#title:Proteins that interact with viral proteins
#tags:interaction,federated,UniProt
#acs:P05556,P08047,P29590
#count:808
#time:0.4
#comment: Just misses Intact SILVER interactions

PREFIX up:<http://purl.uniprot.org/core/>
PREFIX taxon:<http://purl.uniprot.org/taxonomy/>

select distinct ?entry  where {
  ?entry :isoform ?iso.
  {?iso :interactionInfo / rdfs:comment ?txt.
  filter(contains(?txt,"viral") || contains(?txt,"virus")).}
UNION
  {  ?iso :keyword / :term cv:KW-0945.} #host-virus interaction
UNION
  {
   SERVICE <http://beta.sparql.uniprot.org/sparql> {
     SELECT distinct ?humaninteractor WHERE
    {
	?protein a up:Protein .
	?protein up:interaction ?intact . 
    ?intact up:xeno true.
    ?intact up:participant / owl:sameAs ?humaninteractor.
    ?humaninteractor up:organism taxon:9606 .
  	?protein up:organism ?tax .
	?tax up:scientificName ?orgname.
	filter(contains(?orgname,"virus"))
    }
   }
  BIND (IRI(CONCAT("http://nextprot.org/rdf/entry/NX_",substr(str(?humaninteractor),33,6))) as ?entry)	 
  } 
}