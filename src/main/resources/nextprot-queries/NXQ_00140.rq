#id:NXQ_00140
#title:Proteins that interact with viral proteins
#tags:interaction,PPI,federated,UniProt
#acs:P05556,P08047,P29590
#count:2712
#time:3.1

PREFIX up:<http://purl.uniprot.org/core/>
PREFIX taxon:<http://purl.uniprot.org/taxonomy/>

select distinct ?entry where {
  ?entry :isoform ?iso.
  {?iso :interactionInfo / rdfs:comment ?txt.
  filter(contains(?txt,"viral") || contains(?txt,"virus")).}
UNION
  {  ?iso :keyword / :term cv:KW-0945.} #host-virus interaction
UNION
  {
   SERVICE <http://beta.sparql.uniprot.org/sparql> {
     SELECT distinct ?viralinteractor WHERE
    {
	?viralinteractor a up:Protein .
	?viralinteractor rdfs:seeAlso ?db .
	?db up:database <http://purl.uniprot.org/database/IntAct> .
  	?viralinteractor up:organism ?tax .
	?tax up:scientificName ?orgname.
	filter(contains(?orgname,"virus"))
    }
   }
  ?entry :isoform / :binaryInteraction / :interactant ?interactant.
  ?interactant skos:exactMatch ?viralinteractor .
  } 
}
