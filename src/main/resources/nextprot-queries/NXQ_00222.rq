#id:NXQ_00222
#title:Proteins with both RNA-seq expression and IHC expression level "high" in brain or one of its subparts
#tags:expression,tutorial
#acs:Q8IYJ0,P49770,Q2KHR2
#count:1407
#time:13

select distinct ?entry where {
  ?entry :isoform ?iso .
  ?iso :expression ?exprna, ?expihc.
  ?exprna :term ?rnatiss .
  ?exprna :evidence ?evirna.
  ?evirna :expressionLevel :Positive.
  ?evirna :experimentalContext / :detectionMethod cv:ECO_0000295. # RNA-seq
  ?rnatiss :childOf cv:TS-0095. #brain
  ?expihc :quality :GOLD .
  ?expihc :term ?ihctiss .
  ?expihc :evidence ?eviihc.
  ?eviihc :experimentalContext / :detectionMethod cv:ECO_0001055. #IHC
  ?eviihc :expressionLevel :High.
  ?ihctiss :childOf ?rnatiss .
}
