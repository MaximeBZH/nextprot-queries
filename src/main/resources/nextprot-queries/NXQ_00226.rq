#id:NXQ_00226
#title:Proteins with at least 2 validating peptides >=9aa found in blood plasma, urine or cerebrospinal fluid (criteria for biomarker)].
#tags:peptide,mass spectrometry,proteomics,proteotypic,dataset,tutorial
#acs:entry:Q8WYA6,Q8N8Y2,P13746
#count:8793
#time:1476

select distinct ?entry where { 
values ?pepsources { 
        source:PeptideAtlas_human_Cerebrospinal_Fluid 
        source:PeptideAtlas_human_Blood_Plasma 
        source:PeptideAtlas_human_Urine 
        } 
  ?entry :isoform ?iso. 
  ?iso :peptideMapping ?pm, ?pm2 . 
  ?pm :proteotypic true . 
  ?pm  :start ?p1 ; :end ?p2 . 
  filter(?p2-?p1 >= 8) # peptide length >= 9 
  ?pm :evidence / :assignedBy ?pepsources . 
  ?pm2 :evidence / :assignedBy ?pepsources . 
  ?pm2  :start ?p21 ; :end ?p22 .
  filter(?pm != ?pm2)
  bind ( if(?p22<?p2, ?p22, ?p2) - if(?p21>?p1, ?p21, ?p1) as ?overlap) .
  filter( ?overlap < 0) # the two peptides must not overlap
} 
group by ?entry having(count (distinct ?pm) > 1) # at least two such peptides 