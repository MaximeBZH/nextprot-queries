#id:NXQ_00246
#title:Proteins which are enzymes catalyzing a reaction involving lipids
#tags:enzyme,federated query
#acs:C9JRZ8,O00116,O00154
#count:313
#time:421

PREFIX rh:<http://rdf.rhea-db.org/>
PREFIX ch:<http://purl.obolibrary.org/obo/>

select distinct ?entry where {
  service <https://sparql.rhea-db.org/sparql> {
   SELECT distinct ?chebiac WHERE {
   ?reaction rdfs:subClassOf rh:Reaction .
   ?reaction rh:status rh:Approved .
   ?reaction rh:side ?reactionSide .
   ?reactionSide rh:contains ?participant .
   ?participant rh:compound ?compound .
   ?compound rh:chebi ?chebi .
   ?chebi rdfs:subClassOf+ ch:CHEBI_18059 .
   bind(concat("CHEBI:",substr(str(?chebi),38)) as ?chebix) . 
   bind(strdt(str(?chebix),xsd:string) as ?chebiac) . 
   } 
  } 
 ?entry :reference ?x . 
 ?x a :Xref .
 ?x :accession ?chebiac .
 ?x :provenance db:ChEBI .   
 } order by ?entry