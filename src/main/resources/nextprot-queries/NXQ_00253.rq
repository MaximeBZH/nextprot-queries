#id:NXQ_00253
#title:Human pathways in which at least one protein is mitochondrial GOLD
#tags:federated query,gene,pathway,snorql-only,subcellular location,tutorial
#acs:22q11.2 Deletion Syndrome... 
#count:527
#time:250

PREFIX wp: <http://vocabularies.wikipathways.org/wp#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX dcterms: <http://purl.org/dc/terms/>

select distinct ?pathwayname group_concat(distinct ?gen , ',') as ?genes where {
service <http://sparql.wikipathways.org/sparql> {
SELECT DISTINCT ?pathwayname ?gen WHERE
{ 
  ?geneProduct a wp:GeneProduct .  
  ?geneProduct rdfs:label ?gen . 
  ?geneProduct dcterms:isPartOf ?pathway . 
  ?pathway a wp:Pathway . 
  ?pathway wp:organism ?organism . 
  FILTER(contains(str(?organism),"9606"))
  ?pathway dc:title ?pathwayname . 
} 
}
?entry a :Entry .
?entry :gene / :name ?gen .
?entry :isoform / :cellularComponent ?loc . 
values ?mitoloc {cv:SL-0173 cv:GO_0005739 } # SL and GO values for mitochondrion
?loc :term / :childOf ?mitoloc.  # mitochondrion
?loc :evidence / :quality :GOLD .
filter not exists {?loc :negativeEvidence ?negev} # No negative localization evidence
}
order by ?pathwayname