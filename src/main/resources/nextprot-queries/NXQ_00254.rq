#id:NXQ_00254
#title:neXtProt entries with associated pathways in WikiPathways
#tags:federated query,pathway,snorql-only,tutorial
#acs:O60674,P23458,P84022
#count:47426 (11021 entries ans 1000 pathways)
#time:1400

PREFIX wp: <http://vocabularies.wikipathways.org/wp#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX dcterms: <http://purl.org/dc/terms/>

select distinct ?entry ?gen ?pathwayname where {
service <http://sparql.wikipathways.org/sparql> {
SELECT DISTINCT ?pathwayname ?gen WHERE
  { 
  {?geneProduct a wp:Protein}
    union
  {?geneProduct a wp:GeneProduct}
  
  ?geneProduct rdfs:label ?gen . 
  ?geneProduct dcterms:isPartOf ?pathway . 
  ?pathway a wp:Pathway . 
  ?pathway wp:organism ?organism . 
  FILTER(contains(str(?organism),"9606")) # Human proteins
  ?pathway dc:title ?pathwayname . 
  #FILTER(regex(?pathwayname,"cancer","i")). # Can be used to restrict pathways, sometimes cancer keyword is missing (e.g. lymphome)
   }
}
?entry a :Entry .
?entry :gene / :name ?gen .
}
order by ?pathwayname
