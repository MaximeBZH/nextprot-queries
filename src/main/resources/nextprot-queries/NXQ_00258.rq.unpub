#id:NXQ_00258
#title:Diseases caused by intronic variants and associated proteins
#tags:disease,federated query,snorql-only,tutorial,variant
#acs:Q14264,O00255,O75478 
#count:12756
#time:36

PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX sio: <http://semanticscience.org/resource/>
PREFIX ncit: <http://ncicb.nci.nih.gov/xml/owl/EVS/Thesaurus.owl#>
PREFIX so: <http://www.sequenceontology.org/miso/current_svn/term/SO:>

select distinct ?diseaseTitle ?entry str(?gen) as ?gene where {
  service <http://rdf.disgenet.org/sparql/> {
    select distinct ?protein ?diseaseTitle where {
    ?vda sio:SIO_000628 ?variant,?disease .
    ?disease a sio:SIO_010299 ; dcterms:title ?diseaseTitle . # true disease, use ncit:C7057 for traits
    ?variant a so:0001627 ; dcterms:title ?variantTitle . # intron variant
    ?variant so:associated_with ?gene .
        ?gene a ncit:C16612; sio:SIO_010078 / skos:exactMatch ?protein .
    filter(contains(str(?protein),"purl.uniprot"))
    }
  }
  BIND(IRI(replace(str(?protein),"purl","www")) AS ?unipage) .
  ?entry :swissprotPage ?unipage .
  ?entry :gene / :name ?gen .
  #filter not exists {?entry :Isoform / :disease ?_}
} order by ?diseaseTitle
