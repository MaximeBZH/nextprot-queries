#id:NXQ_00259
#title:Proteins involved in long organs phenotypes with PMIDs
#tags:disease,evidence,federated query,phenotype,publication,tutorial
#acs:Q02809,Q09428,Q6KC79 
#count:8
#time:0.4

PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX sio: <http://semanticscience.org/resource/>
PREFIX ncit: <http://ncicb.nci.nih.gov/xml/owl/EVS/Thesaurus.owl#>

select distinct ?entry (str(?gen) as ?genx) ?umlsTerm  ?samplepubmed where {
  service <http://rdf.disgenet.org/sparql/> {
    select distinct ?protein ?umlsTerm (sample(?article) as ?samplepubmed)
    where {
    ?gda sio:SIO_000628 ?gene,?disease .
    ?gda sio:SIO_000772 ?article .
    ?disease a sio:SIO_010056 .
    ?disease dcterms:title ?umlsTerm .
    filter(contains(str(?umlsTerm),"Long "))
        ?gene a ncit:C16612; sio:SIO_010078 / skos:exactMatch ?protein .
    filter(contains(str(?protein),"purl.uniprot"))
      } group by ?protein ?umlsTerm
    }
  BIND(IRI(replace(str(?protein),"purl","www")) AS ?unipage) .
  ?entry :swissprotPage ?unipage .
  ?entry :gene / :name ?gen .
} order by ?entry
