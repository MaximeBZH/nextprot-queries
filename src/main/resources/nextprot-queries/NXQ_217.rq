#id:NXQ_00217
#title:Proteins associated with an incomplete EC number, no function annotated and high or specific expression in liver
#tags:expression,function,
#acs:NX_P49768,...
#count:19
#time:0.7

select distinct ?entry where {
  ?entry :isoform ?iso.
  ?iso :enzymeClassification / :term / rdfs:label ?eclabel.
  filter regex(?eclabel, '-'^^xsd:string) # incomplete EC number
  ?iso :expression ?e1.
  ?e1 :term /:childOf cv:TS-0564; :evidence/:expressionLevel :High. # highly expressed in liver
   { 
   ?iso :functionInfo / rdfs:comment ?functext .
   filter(strstarts(?functext,"Probable") || strstarts(?functext,"Putative")) 
   }
  UNION
   { 
   filter not exists {?iso :functionInfo ?_ }  # entries with no function at all
   }
} 